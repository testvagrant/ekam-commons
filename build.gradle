plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.testvagrant.ekam.commons'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    //google
    implementation('com.google.guava:guava:30.1.1-jre')
    implementation('com.google.inject:guice:7.0.0')
    implementation('com.google.code.gson:gson:2.10.1')

    // Apache commons-io
    implementation('commons-io:commons-io:2.13.0')

    //lombok
    implementation('org.projectlombok:lombok:1.18.28')
    annotationProcessor('org.projectlombok:lombok:1.18.28')
    testImplementation('org.projectlombok:lombok:1.18.28')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.28')

    // Test Framework
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.0-M1')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.8.0-M1')
    testImplementation('org.junit-pioneer:junit-pioneer:1.4.2')

    testImplementation('org.assertj:assertj-guava:3.24.2')
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.testvagrant.ekam'
            artifactId 'ekam-commons'
            version '1.0.2'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv("sonatypeUsername")
                password System.getenv("sonatypePassword")
//                username sonatypeUsername
//                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Ekam commons for data, file, i8 and cache management'
            name 'Ekam Common'
            url 'https://github.com/testvagrant/ekam-commons'
            organization {
                name 'com.testvagrant.ekam'
                url 'https://github.com/testvagrant'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/testvagrant/ekam-commons/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/testvagrant/ekam-commons/-/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/testvagrant/ekam-commons'
                connection 'https://github.com/testvagrant/ekam-commons.git'
                developerConnection 'git@github.com:testvagrant/ekam-commons.git'
            }
            developers {
                developer {
                    name 'Sudarshan GS'
                }
                developer {
                    name 'Krishnanand B'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}

signing {
    sign publishing.publications
}
